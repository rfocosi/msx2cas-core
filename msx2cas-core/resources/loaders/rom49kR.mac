.Z80

; BLOAD MODULE FOR 49KB ROMS PART 3 WITH RESET (FOR CASLINK3 PROJECT)
; COPYRIGHT (C) 1999-2016 ALEXEY PODREZOV
; Modified by Roberto Focosi for MSX2Cas Project

	ASEG
	ORG	9000h

START:	JP	START1

STARTA:	DW	00
ENDA:	DW	00
EXECA:	DW	00
CRC:	DB	00

CASERR:	DB	"< MSX2Cas > Fail: CRC ERROR!",0

START1:	DI
	LD	HL,(STARTA)
      	LD	DE,(ENDA)
	EX	DE,HL
	SCF
	CCF
	SBC	HL,DE
	PUSH	HL
	POP	BC
	LD	HL,ROMCODE
        XOR	A
	PUSH	AF
START2:	POP	AF
	ADD	A,(HL)
	INC	HL
	DEC	BC
	PUSH	AF
	LD	A,C
	OR	A
	JR	NZ,START2
	LD	A,B
	OR	A
	JR	NZ,START2
	POP	AF
	LD	B,A
	LD	HL,CRC
	LD	A,(HL)
	CP	B
	JP	Z,START5

CRCERR:	EI
	CALL	006CH		; set screen 0
	LD	A,0FH
	LD	HL,0F3E9H
	LD	(HL),A
	LD	A,8
	INC	HL
	LD	(HL),A
	INC	HL
	LD	(HL),A
	CALL	0062H		; set color 15,8,8
	XOR	A
	CALL	00C3H		; clear screen
	CALL	00CFH		; unhide functional keys
	LD	HL,0101H
	CALL	00C6H		; set cursor position to 1:1
	LD	DE,CASERR
START3:	LD	A,(DE)
	OR	A
	JR	Z,START4
	INC	DE
	CALL	00A2H		; display character
	INC	H
	CALL	00C6H		; set next position
	JR	START3

START4:	LD	HL,0103H
	CALL	00C6H		; set cursor position to 1:3
	CALL	00C0H		; beep
	CALL	0156H		; clears keyboard buffer
	RET

START5:	DI
	LD	A,(0FFFFH)
	CPL
	LD	C,A
	AND	0F0H
	LD	B,A
	LD	A,C
	RRCA
	RRCA
	RRCA
	RRCA
	AND	15
	OR	B
	LD	(0FFFFH),A
	IN	A,(0A8h)
	AND	0F0H
	LD	B,A
	RRCA
	RRCA
	RRCA
	RRCA
	AND	15
	OR	B
	PUSH	AF
	OUT	(0A8h),A

START6: LD	HL,ROMCODE
	LD	DE,START8+5
	LD	A,(HL)
	LD	(DE),A		; transfer byte from 8000H to patcher
	INC	HL
	LD	DE,START8+9
	LD	A,(HL)
	LD	(DE),A		; transfer byte from 8001H to patcher
	INC	HL
	LD	DE,START8+13
	LD	A,(HL)
	LD	(DE),A		; transfer byte from 8002H to patcher
	LD	HL,(EXECA)
	PUSH	HL
	LD	(START8+16),HL	; transfer start address to patcher
	LD	A,(HL)
	LD	DE,START8+20
	LD	(DE),A		; transfer 1st byte from EPA
	INC	HL
	LD	A,(HL)
	LD	DE,START8+24
	LD	(DE),A		; transfer 2nd byte from EPA
	INC	HL
	LD	A,(HL)
	LD	DE,START8+28
	LD	(DE),A		; transfer 3rd byte from EPA
	POP	HL
	LD	A,0CDH
	LD	(HL),A		; place call opcode at EPA
	INC	HL
	PUSH	HL
	LD	HL,ROMCODE+4
	LD	DE,8000H
	SCF
	CCF
	SBC	HL,DE		; offset for finding the end of ROM+4
	LD	DE,(ENDA)
	SCF
	CCF
	ADC	HL,DE
	EX	DE,HL
	PUSH	DE
	LD	HL,START8
	LD	BC,33
	LDIR			; transfer patcher to the end of ROM+4
	LD	HL,(ENDA)
	LD	DE,37
	SCF
	CCF
	ADC	HL,DE
	LD	(ENDA),HL	; adjust ROM's end address with patcher size
	POP	DE
	POP	HL
	LD	A,E
	LD	(HL),A		; save the low byte of call address
	INC	HL
	LD	A,D
	LD	(HL),A		; save the high byte of call address

	LD	HL,START7
      	LD	DE,START8+1
	EX	DE,HL
	SCF
	CCF
	SBC	HL,DE
	LD	B,H
	LD	C,L
	LD	HL,START7
	LD	DE,0F560H
	PUSH	DE
	LDIR
	RET

START7:	LD	HL,(STARTA)
      	LD	DE,(ENDA)
	EX	DE,HL
	SCF
	CCF
	SBC	HL,DE
	LD	B,H
	LD	C,L
	LD	HL,ROMCODE
	LD	DE,(STARTA)
	LDIR
	POP	AF
	AND	0FCH
	OUT	(0A8H),A
	RST	30H
	DW	0
	DW	0
	NOP

START8:	DI
	LD	HL,8000H
	LD	A,00		; +5 bytes
	LD	(HL),A
	INC	HL
	LD	A,00 		; +9 bytes
	LD	(HL),A
	INC	HL
	LD	A,00		; +13 bytes
	LD	(HL),A
	LD	HL,0000H	; +16 bytes
	PUSH	HL
	LD	A,00		; +20 bytes
	LD	(HL),A
	INC	HL
	LD	A,00		; +24 bytes
	LD	(HL),A
	INC	HL
	LD	A,00  		; +28 bytes
	LD	(HL),A
	POP	HL
	JP	(HL)
	NOP

ROMCODE:

END
