.Z80

; BLOAD MODULE FOR 8/16KB ROMS WITH RESET (FOR CASLINK3 PROJECT)
; COPYRIGHT (C) 1999-2016 ALEXEY PODREZOV
; Modified by Roberto Focosi for MSX2Cas Project

	ASEG
	ORG	9000h

START:	JP	START1

STARTA:	DW	00
ENDA:	DW	00
EXECA:	DW	00
CRC:	DB	00

CASERR:	DB	"< MSX2Cas > Fail: CRC ERROR!",0

START1:	DI
	LD	HL,(STARTA)
      	LD	DE,(ENDA)
	EX	DE,HL
	SCF
	CCF
	SBC	HL,DE
	PUSH	HL
	POP	BC
	LD	HL,ROMCODE
        XOR	A
	PUSH	AF
START2:	POP	AF
	ADD	A,(HL)
	INC	HL
	DEC	BC
	PUSH	AF
	LD	A,C
	OR	A
	JR	NZ,START2
	LD	A,B
	OR	A
	JR	NZ,START2
	POP	AF
	LD	B,A
	LD	HL,CRC
	LD	A,(HL)
	CP	B
	JP	Z,START5	

CRCERR:	EI
	CALL	006CH		; set screen 0
	LD	A,0FH
	LD	HL,0F3E9H
	LD	(HL),A
	LD	A,8
	INC	HL
	LD	(HL),A
	INC	HL
	LD	(HL),A
	CALL	0062H		; set color 15,8,8
	XOR	A
	CALL	00C3H		; clear screen
	CALL	00CFH		; unhide functional keys
	LD	HL,0101H
	CALL	00C6H		; set cursor position to 1:1
	LD	DE,CASERR
START3:	LD	A,(DE)
	OR	A
	JR	Z,START4
	INC	DE
	CALL	00A2H		; display character
	INC	H
	CALL	00C6H		; set next position
	JR	START3

START4:	LD	HL,0103H
	CALL	00C6H		; set cursor position to 1:3
	CALL	00C0H		; beep
	CALL	0156H		; clears keyboard buffer
	EI
	RET

START5:	DI
	LD	A,(0FFFFh)
	CPL
	AND	0F0H
	LD	C,A
	RRCA
	RRCA
	RRCA
	RRCA
	AND	15
	OR	C
	LD	(0FFFFh),A
	IN	A,(0A8h)
	AND	0F0H
	LD	B,A
	RRCA
	RRCA
	RRCA
	RRCA
	AND	15
	OR	B
	PUSH	AF
	OUT	(0A8h),A

START6: LD	HL,(STARTA)
	LD	A,H
	CP	40H
	JR	C,START7
	CP	80H
	JR	C,START8
START7:	LD	HL,4000H
	LD	A,41H
	LD	(HL),A
	INC	A
	INC	HL
	LD	(HL),A		; set AB at 4000H
	INC	HL
	LD	DE,(EXECA)
	LD	(HL),E
	INC	HL
	LD	(HL),D		; set start address at 4002H
	LD	HL,START8
      	LD	DE,ROMCODE+1
	EX	DE,HL
	SCF
	CCF
	SBC	HL,DE
	LD	B,H
	LD	C,L
	LD	HL,START8
	LD	DE,0F560H
	PUSH	DE
	LDIR
	RET

START8:	LD	HL,(STARTA)
      	LD	DE,(ENDA)
	EX	DE,HL
	SCF
	CCF
	SBC	HL,DE
	LD	B,H
	LD	C,L
	LD	HL,ROMCODE
	LDIR
	POP	AF
	AND	0FCH
	OUT	(0A8H),A
	RST	30H
	DW	0
	DW	0
	NOP
	
ROMCODE:

END
