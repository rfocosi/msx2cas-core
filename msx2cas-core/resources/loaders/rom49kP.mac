.Z80

; PRE-BLOAD MODULE FOR MULTIFILE MODE (FOR CASLINK3 PROJECT)
; COPYRIGHT (C) 1999-2016 ALEXEY PODREZOV
; Modified by Roberto Focosi for MSX2Cas Project

	ASEG
	ORG	9000h


START:	JP	START1

LOADCMD:DB	01EH
	DB	"bload"
	DB	34
	DB	"cas:      "
	DB	34
	DB	",r"
	DB	13,0

CASLSTR:DB	"< MSX2Cas > Loading, please wait...",0

START1:	EI
	CALL	006CH		; set screen 0
	LD	A,0FH
	LD	HL,0F3E9H
	LD	(HL),A
	LD	A,4
	INC	HL
	LD	(HL),A
	INC	HL
	LD	(HL),A
	CALL	0062H		; set color 15,4,4
	XOR	A
	CALL	00C3H		; clear screen
	CALL	00CCH		; hide functional keys
	LD	HL,0101H
	CALL	00C6H		; set cursor position to 1:1
	LD	DE,CASLSTR
START2:	LD	A,(DE)
	OR	A
	JR	Z,START3
	INC	DE
	CALL	00A2H		; display character
	INC	H
	CALL	00C6H		; set next position
	JR	START2

START3:	LD	HL,0103H
	CALL	00C6H		; set cursor position to 1:3
	CALL	0156H		; clears keyboard buffer
	DI
	LD	HL,0FBF0H
	LD	(0F3F8H),HL
	LD	(0F3FAH),HL
	LD	HL,LOADCMD
	LD	DE,0FBF0H
	LD	BC,29
	LDIR			; send command to buffer
	LD	HL,0FBF0H+29
	LD	(0F3F8H),HL
	EI
	RET
	NOP
	
END

