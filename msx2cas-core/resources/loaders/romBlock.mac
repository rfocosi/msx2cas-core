.Z80

; BLOAD MODULE FOR 32KB ROMS PART 1 (FOR CASLINK3 PROJECT)
; COPYRIGHT (C) 1999-2016 ALEXEY PODREZOV
; Modified by Roberto Focosi for MSX2Cas Project
; BLOAD MODULE FOR ROM'S FIRST BLOCKS

	ASEG
	ORG	9000h

START:	JP	START1

STARTA:	DW	00
ENDA:	DW	00
EXECA:	DW	00
CRC:	DB	00

LOADCMD:DB	01EH
	DB	"bload"
	DB	34
	DB	"cas:"
	DB	34
	DB	",r"
	DB	13,0

CASLSTR:DB	"< MSX2Cas > Loading, please wait...",0

CASERR:	DB	"< MSX2Cas > Fail: CRC ERROR!",0

START1:	DI
	LD	HL,(STARTA)
      	LD	DE,(ENDA)
	EX	DE,HL
	SCF
	CCF
	SBC	HL,DE
	PUSH	HL
	POP	BC
	LD	HL,ROMCODE
        XOR	A
	PUSH	AF
START2:	POP	AF
	ADD	A,(HL)
	INC	HL
	DEC	BC
	PUSH	AF
	LD	A,C
	OR	A
	JR	NZ,START2
	LD	A,B
	OR	A
	JR	NZ,START2
	POP	AF
	LD	B,A
	LD	HL,CRC
	LD	A,(HL)
	CP	B
	JP	Z,START5	

CRCERR:	EI
	CALL	006CH		; set screen 0
	LD	A,0FH
	LD	HL,0F3E9H
	LD	(HL),A
	LD	A,8
	INC	HL
	LD	(HL),A
	INC	HL
	LD	(HL),A
	CALL	0062H		; set color 15,8,8
	XOR	A
	CALL	00C3H		; clear screen
	CALL	00CFH		; unhide functional keys
	LD	HL,0101H
	CALL	00C6H		; set cursor position to 1:1
	LD	DE,CASERR
START3:	LD	A,(DE)
	OR	A
	JR	Z,START4
	INC	DE
	CALL	00A2H		; display character
	INC	H
	CALL	00C6H		; set next position
	JR	START3

START4:	LD	HL,0103H
	CALL	00C6H		; set cursor position to 1:3
	CALL	00C0H		; beep
	CALL	0156H		; clears keyboard buffer
	RET

START5:	EI
	CALL	006CH		; set screen 0
	LD	A,0FH
	LD	HL,0F3E9H
	LD	(HL),A
	LD	A,4
	INC	HL
	LD	(HL),A
	INC	HL
	LD	(HL),A
	CALL	0062H		; set color 15,4,4
	XOR	A
	CALL	00C3H		; clear screen
	CALL	00CCH		; hide functional keys
	LD	HL,0101H
	CALL	00C6H		; set cursor position to 1:1
	LD	DE,CASLSTR
START6:	LD	A,(DE)
	OR	A
	JR	Z,START7
	INC	DE
	CALL	00A2H		; display character
	INC	H
	CALL	00C6H		; set next position
	JR	START6

START7:	LD	HL,0103H
	CALL	00C6H		; set cursor position to 1:3
	CALL	0156H		; clears keyboard buffer
	DI
	LD	HL,0FBF0H
	LD	(0F3F8H),HL
	LD	(0F3FAH),HL
	LD	HL,LOADCMD
	LD	DE,0FBF0H
	LD	BC,29
	LDIR			; send command to buffer
	LD	HL,0FBF0H+29
	LD	(0F3F8H),HL

LOADROM:DI
	LD	A,(0FFFFH)
	CPL
	PUSH	AF
	LD	C,A
	AND	0F0H
	LD	B,A
	LD	A,C
	RRCA
	RRCA
	RRCA
	RRCA
	AND	15
	OR	B
	LD	(0FFFFH),A
	IN	A,(0A8h)
	PUSH	AF
	AND	0F0H
	LD	B,A
	RRCA
	RRCA
	RRCA
	RRCA
	AND	15
	OR	B
	OUT	(0A8h),A

	LD	HL,(STARTA)
      	LD	DE,(ENDA)
	EX	DE,HL
	SCF
	CCF
	SBC	HL,DE
	LD	B,H
	LD	C,L
	LD	HL,ROMCODE
	LD	DE,(STARTA)
	LDIR
	POP	AF
	OUT	(0A8H),A
	POP	AF
	LD	(0FFFFH),A
	EI
	RET
	NOP

ROMCODE:

END
